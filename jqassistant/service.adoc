[[service:Default]]
[role=group,includesConcepts="service:RemoteBeanInterface",includesConstraints="service:ServiceInterfacesEndenAufRemoteBean,service:ExceptionAnServiceSchnitstelle,service:ServiceSchnitstelleMitLoggingKontext,service:ServiceMethodenEnthaltenAufrufKontext"]

== Service

Dieser Abschnitt beschreibt die Regeln zur Implementierung von Services.

=== Konzepte

[[service:RemoteBeanInterface]]
.Markiert Interfaces, deren Name mit `RemoteBean` endet, mit `Service:RemoteBeanInterface`.
[source,cypher,role=concept]
----
MATCH
  (i:Interface)<-[:CONTAINS*]-(serviceLayer:Layer)
WHERE
  serviceLayer.name = "service"
  AND i.name ENDS WITH "RemoteBean"
SET
  i:Isy:Service:RemoteBeanInterface
RETURN
  i AS RemoteBeanInterface
----

=== Regeln

[[service:ServiceInterfacesEndenAufRemoteBean]]
.Schnittstellen Interface-Klassennamen enden auf `RemoteBean`.
[source,cypher,role=constraint,requiresConcept="layer-general:DefinedLayer"]
----
MATCH
  (i:Interface)<-[:CONTAINS*]-(serviceLayer:Layer)
WHERE
  serviceLayer.name = "service"
  AND NOT i.name ENDS WITH "RemoteBean"
RETURN
  i AS ServiceInterfaceOhneRemoteBeanEndung
----

[[service:ExceptionAnServiceSchnitstelle]]
.Exception-Behandlung: Tritt bei der Verarbeitung eines Service-Aufrufs im Anwendungskern oder in der Komponente "Service" eine Exception auf, so muss diese Exception in eine Exception der Service-Schnittstelle umgewandelt werden. Es werden keine Exceptions des Anwendungskerns geworfen.
[source,cypher,role=constraint,requiresConcept="service:RemoteBeanInterface"]
----
MATCH
  (rbi:RemoteBeanInterface)-[:DECLARES]->(m:Method),
  (m)-[:THROWS]->(exception:Type)
WHERE
  NOT (:Layer {name: "service"})-[:CONTAINS*]->(exception)
RETURN
  rbi AS RemoteBeanInterface, m AS Methode, exception AS Exception
----

[[service:ServiceSchnitstelleMitLoggingKontext]]
.Wichtig ist, in der Exception-Fassade an der Service-Schnittstelle die Correlation-ID zu setzen.
[source,cypher,role=constraint,requiresConcept="service:RemoteBeanInterface"]
----
MATCH
  (remoteBeanImpl:Type)-[:IMPLEMENTS]->(:RemoteBeanInterface),
  (remoteBeanImpl)-[:DECLARES]->(m:Method),
  ()-[:ANNOTATED_BY]->(:Annotation)-[:OF_TYPE]->(stelltLoggingKontextBereit:Type)
WHERE
  stelltLoggingKontextBereit.name = "StelltLoggingKontextBereit"
  AND m.name <> "<init>"
  AND NOT (m)-[:ANNOTATED_BY]->()-[:OF_TYPE]->(stelltLoggingKontextBereit)
RETURN
  remoteBeanImpl AS RemoteBeanImpl, m AS MethodeOhneLoggingKontext
----

[[service:ServiceMethodenEnthaltenAufrufKontext]]
.Jede Methode der RemoteBean-Schnittstelle muss als ersten Parameter ein Objekt der Klasse `AufrufKontextTo` bzw. `ClientAufrufKontextTo` verwenden.
[source,cypher,role=constraint,requiresConcept="service:RemoteBeanInterface"]
----
MATCH
  (rbi:RemoteBeanInterface)-[:DECLARES]->(m:Method)
WHERE
  NOT m.signature =~ ".+\\([a-z\\.0-9_]+(AufrufKontextTo|ClientAufrufKontextTo)(,.+|\\))?"
RETURN
  m.signature AS MethodeOhneAufrufKontextTo
----
